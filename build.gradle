plugins {
    id 'java-library'
    id 'io.qameta.allure' version '2.10.0'
}

allure {
    report {
        version.set("2.19.0")
    }
    adapter { // отвечает за появление папочки build/allure-results
        aspectjWeaver.set(true) //обработка аннотации @Step
        frameworks {
            junit5 { //название фреймворка
                adapterVersion.set("2.19.0") //версия интеграции фреймворка и Allure
            }
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation(
            "com.codeborne:selenide:6.3.5",
            "com.github.javafaker:javafaker:1.0.2",
            "org.junit.jupiter:junit-jupiter:5.8.2",
            'org.assertj:assertj-core:3.23.1',
            'com.codeborne:pdf-test:1.5.0',
            'com.codeborne:xls-test:1.4.3',
            'com.opencsv:opencsv:5.7.1',
            'com.fasterxml.jackson.core:jackson-core:2.13.1',
            'com.fasterxml.jackson.core:jackson-databind:2.13.1',
            'io.qameta.allure:allure-selenide:2.19.0',
            "org.aspectj:aspectjrt:1.9.8",
            "org.aspectj:aspectjweaver:1.9.8")
    testRuntimeOnly(
            "org.junit.jupiter:junit-jupiter:5.8.2",
            "org.slf4j:slf4j-simple:1.7.29")
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(Test) {
    systemProperties(System.getProperties())
    useJUnitPlatform()

    testLogging {
        lifecycle {
            events "started", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat "short"
        }
    }
}

test {
    useJUnitPlatform()
}

task remote_test(type: Test) {
    useJUnitPlatform {
        includeTags("remote")
    }
}

task param_test(type: Test) {
    useJUnitPlatform {
        includeTags("parameters")
    }
}